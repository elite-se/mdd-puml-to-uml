grammar elite.mdd.plantuml.PlantUML with org.eclipse.xtext.common.Terminals

generate plantUML "http://www.mdd.elite/plantuml/PlantUML"

Diagram:
	'@startuml' (elements+=SequenceElement)+ '@enduml';
	
SequenceElement:
	ParticipantDefinition
	| MessageDefinition;
	
// Participants
	
ParticipantDefinition:
	shape=ParticipantShape participant=Participant;
	
Participant:
	'"'name=ID'"';
	
enum ParticipantShape:
	ACTOR='actor' 
	| BOUNDARY='boundary' 
	| CONTROL='control' 
	| COLLECTIONS='collections' 
	| ENTITY='entity'
	| DATABASE='database'
	| PARTICIPANT='participant';
	
// Messages
	
MessageDefinition:
	RequestMessageDefinition
	| ReplyMessageDefinition;
	
RequestMessageDefinition:
	('"'sender=[Participant]'"' arrow=(RightRequest) '"'receiver=[Participant]'"') (':' message=RequestMessage)
	| ('"'receiver=[Participant]'"' arrow=(LeftRequest) '"'sender=[Participant]'"') (':' message=RequestMessage);
	
ReplyMessageDefinition:
	('"'sender=[Participant]'"' arrow=RightReply '"'receiver=[Participant]'"') (':' message=ReplyMessage)
	| ('"'receiver=[Participant]'"' arrow=LeftReply '"'sender=[Participant]'"') (':' message=ReplyMessage);
	
// TODO: Handle \"	
RequestMessage:
	name=ID ('('(arguments+=RequestMessageArgument (',' arguments+=RequestMessageArgument)*)?')')?;	

RequestMessageArgument:
	value=Value
	| name=ID "=" value=Value 
	;
	
ReplyMessage:
	name=ID ('('(arguments+=ReplyMessageArgument (',' arguments+=ReplyMessageArgument)*)?')')?
	| variable=ID "=" name=ID ('('(arguments+=ReplyMessageArgument (',' arguments+=ReplyMessageArgument)*)?')')? (':' value=Value)?
	;
	
ReplyMessageArgument:
	name=ID ":" type=ID
	| name=ID "=" type=ID (":" value=Value)? 
	;
	
Value:
	INT
	| STRING
	;
	
enum RequestArrow:
	RIGHT_SYNC
	| RIGHT_ASYNC
	| LEFT_SYNC
	| LEFT_ASYNC;

enum RightRequest returns RequestArrow:
	RIGHT_SYNC='->'
	| RIGHT_ASYNC='->>';

enum LeftRequest returns RequestArrow:
	LEFT_SYNC='<-'
	| LEFT_ASYNC='<<-';
	
enum ReplyArrow:
	RIGHT_REPLY
	| LEFT_REPLY;
	
enum RightReply returns ReplyArrow:
	RIGHT_REPLY='-->>';
	
enum LeftReply returns ReplyArrow:
	LEFT_REPLY='<<--'
;
	
// TODO: Handle Strings correctly
@Override 
terminal STRING:
			"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'"
		;
	
	